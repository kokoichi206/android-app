plugins {
    id 'com.android.application'
    id 'kotlin-android'

    id 'kotlin-kapt'
    id 'kotlin-android-extensions'
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    dataBinding {
        enabled true
    }

    defaultConfig {
        applicationId "io.kokoichi.sample.mastodonclient"
        minSdkVersion 19
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        multiDexEnabled true // 64k をコスものに対して必要

        // access token
        def instanceProperties = project.rootProject.file('instance.properties')
        if (!instanceProperties.exists()) {
            instanceProperties.createNewFile()
        }

        def prop = new Properties()
        prop.load(project.rootProject.file('instance.properties').newDataInputStream())
        def INSTANCE_URL = prop.getProperty("instance_url") ?: ""
        def USERNAME = prop.getProperty("username") ?: ""
        def ACCESS_TOKEN = prop.getProperty("access_token") ?: ""

        buildConfigField("String", "INSTANCE_URL", "\"${INSTANCE_URL}\"")
        buildConfigField("String", "USERNAME", "\"${USERNAME}\"")
        buildConfigField("String", "ACCESS_TOKEN", "\"${ACCESS_TOKEN}\"")
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    packagingOptions {
        pickFirst "androidsupportmultidexversion.txt"
    }
}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.1'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.1'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    // These are for fragment View
    def fragment_version = "1.2.4"
    // Java language implementation
    implementation "androidx.fragment:fragment:$fragment_version"
    // Kotlin
    implementation "androidx.fragment:fragment-ktx:$fragment_version"

    implementation 'com.squareup.retrofit2:retrofit:2.7.1' // API
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.3" // コルーチン
    implementation "com.squareup.moshi:moshi-kotlin:1.9.2"  // JSON パース用
    implementation 'com.squareup.retrofit2:converter-moshi:2.7.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'   // 便利なリスト形式表示
    implementation 'com.github.bumptech.glide:glide:4.10.0'     // 画像表示ライブラリ
    kapt 'com.github.bumptech.glide:compiler:4.10.0'

    implementation 'com.android.support:multidex:1.0.3'   // For large build
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.0.0'
    implementation "com.google.android.material:material:1.1.0"
}

